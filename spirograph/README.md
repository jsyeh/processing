

# Spirograph è¬èŠ±å°º

æˆ‘åœ¨ FB çœ‹åˆ°å»£å‘Šå½±ç‰‡, Weicskgl2 åœ¨è³£è¬èŠ±å°ºã€‚
- [FBå½±ç‰‡](https://www.facebook.com/watch/?v=274789904222202) å»£å‘Šè©: ğŸ¨ é€™æ˜¯ä¸€å€‹å¹¾ä¹è¢«éºå¿˜çš„ç«¥å¹´ç©å…· ğŸŒŸæœ‰åˆ©æ–¼åŸ¹é¤Šå­©å­çš„å‹•æ‰‹èƒ½åŠ› âœ¨ é–‹ç™¼æ™ºåŠ›å’Œæ€ç¶­èƒ½åŠ›ğŸğŸ ç«‹å³è·å–ğŸ‘‰https://bit.ly/3smHtiO [è³£å ´](https://tw.weicskgl.com/detail1?id=542)
- [éœ²å¤©spirograph](https://www.ruten.com.tw/find/?q=spirograph)
- [è¦çš®spirograph](https://shopee.tw/search?keyword=spirograph)
- [è¦çš®æœ‰è³£å®¶96å…ƒ](https://shopee.tw/27Pcs-Kids%E5%8E%9F%E8%A3%9DSpirograph%E8%A8%AD%E8%A8%88%E5%9C%96%E7%B4%99%E9%8C%AB%E7%B9%AA%E8%A3%BD%E8%97%9D%E8%A1%93%E5%B7%A5%E8%97%9D%E7%8E%A9%E5%85%B7-KT%E6%AF%8D%E5%AC%B0-i.77521276.4565438982)

é›–ç„¶çœ‹èµ·ä¾†åƒè©é¨™é¢¨æ ¼çš„ä¸€é å¼ç¶²é , ä¸éæˆ‘è¦ºå¾—é€™å€‹ç©å…·å¾ˆå¥½ç©ã€‚æœƒæƒ³è¦å¯«ç¨‹å¼ç•«ç•«çœ‹ã€‚

æˆ‘ä¹‹å‰å¯¦ä½œåˆ°ä¸€åŠå°±åˆ†å¿ƒ, å› ç‚ºçœ‹åˆ°å¾Œé¢æ¼¸çš„è‰²å½©, æˆ‘åè€Œå»æ‰¾èƒŒæ™¯åœ–, é€™æ¨£å°±ä¸è¡Œäº†ã€‚ç¾åœ¨æˆ‘æ‰“ç®—ç”¨ç·šæ¢çš„æ–¹å¼, æ…¢æ…¢ç•«å‡ºä¾†ã€‚
è¦æ¼¸å±¤èƒŒæ™¯çš„è©±, å¯æ‰¾ rainbow scratch paper åˆ®ç•«ç´™ã€‚

https://en.wikipedia.org/wiki/Spirograph

æŒ‰ä¸‹åœ–å¯æ’­æ”¾å½±ç‰‡

[![Spirograph](https://j.gifs.com/mOLWg9.gif)](https://youtu.be/9_6XOOiyvkA)

ç¨‹å¼å¯¦ä½œçš„éç¨‹è¨˜éŒ„
===============
æˆ‘çš„ç¨‹å¼è¨˜éŒ„ [hackmd](https://hackmd.io/@jsyeh/spirograph)

Step01 å…ˆç•«å‡ºå¤§åœ“
---------------
å…ˆç•«å‡ºç°¡å–®çš„å¤§åœ“, é¢¨æ ¼ä½¿ç”¨é»‘åº•ã€ç™½ç·šæ¢, å¸Œæœ›åƒå½±ç‰‡ä¸­çš„é¢¨æ ¼ã€‚
```Processing
void setup(){
  size(500,500);
  stroke(255);
  noFill();
}
void draw(){
  background(0);
  ellipse(250,250, 450,450);
}
```

Step02 mouseç•«å‡ºå°åœ’
-------------------
æ¥è‘—ç”¨ mouse ä¾†ç§»å‹•å°åœ“ã€‚åˆ©ç”¨æ‰‹ä¾†è¼”è‚‹, æ”¾å¥½å°åœ“å¾Œ, æ¯”è¼ƒå®¹æ˜“åšå‡ºè¼”åŠ©ç·š, äº†è§£å°åœ“æ˜¯æ€éº¼ç§»å‹•çš„ã€‚
```Processing
void setup(){
  size(500,500);
  stroke(255);
  noFill();
}
void draw(){
  background(0);
  ellipse(250,250, 450,450);
  ellipse(mouseX, mouseY, 100,100);
}
```

Step03 atan2() ç®—å°åœ“åœ“å¿ƒ
--------------------------
åˆ©ç”¨ mouse åŠ atan2() ç®—å‡ºè§’åº¦, æ¨ç®—å‡ºå°åœ“åœ“å¿ƒå¾Œ
```Processing
void setup(){
  size(500,500);
  stroke(255);
  noFill();
}
void draw(){
  background(0);
  ellipse(250,250, 450,450);
  float angle=atan2(mouseY-250,mouseX-250);
  float x=250+175*cos(angle), y=250+175*sin(angle);
  line(250,250, x, y);
  ellipse(x,y, 100, 100);
}
```

Step04 ç”¨è®Šæ•¸å–ä»£æ•¸å€¼
----------------------
ç”¨è®Šæ•¸å–ä»£æ•¸å€¼ã€‚ä¹‹å¾Œåªè¦æ”¹è®Šå¤§åœ“åŠå¾‘ã€å°åœ“åŠå¾‘, å°±æœ‰æ©Ÿæœƒç•«å‡ºä¸åŒçš„ç·šæ¢ã€‚
```Processing
void setup(){
  size(500,500);
  stroke(255);
  noFill();
}
float r0=225, r1=50;
float cx=250, cy=250;
void draw(){
  background(0);
  ellipse(cx,cy, r0*2, r0*2);
  float angle=atan2(mouseY-cy,mouseX-cx);
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);
  line(cx,cy, x, y);
  ellipse(x,y, r1*2, r1*2);
}
```

Step05 å°åœ“æœƒæ—‹è½‰
----------------
ç‚ºäº†è®“å°åœ“æœ‰æ—‹è½‰çš„æ•ˆæœ, æ‰€ä»¥æ–°å¢ circle2()å‡½å¼, è£¡é¢æœ‰angleåƒæ•¸, å¯ä»¥èª¿æ•´æ—‹è½‰è§’åº¦ã€‚ç¾éšæ®µå…ˆéš¨ä¾¿ç”¨å€‹å€¼ä¾†æ—‹è½‰ã€‚ä¹‹å¾Œæ‡‰è©²ç®—å‡ºåˆç†çš„å€¼ä¾†æ—‹è½‰ã€‚
è¨»: ç‚ºäº†çœ‹åˆ°æ—‹è½‰çš„æ•ˆæœ, å°åœ“æœ‰é™„å¹¾æ¢è¼ªè¼», å°åœ“è¼ªè¼»æ•¸å°‘,è½‰å‹•è¼ƒæ¸…æ¥šã€‚
```Processing
void setup(){
  size(500,500);
  stroke(255);
  noFill();
}
float r0=225, r1=50;
float cx=250, cy=250;
void draw(){
  background(0);
  ellipse(cx,cy, r0*2, r0*2);
  float angle=atan2(mouseY-cy,mouseX-cx);
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);
  line(cx,cy, x, y);
  circle2(x,y, r1, mouseX);//ellipse(x,y, r1*2, r1*2);
}
void circle2(float cx, float cy, float r, float angle){
  ellipse(cx,cy, r*2, r*2);
  for(float a=angle; a<angle+PI*2;a+=PI/4){
    line(cx,cy, cx+r*cos(a), cy+r*sin(a));
  }
}
```

Step06 è¨ˆç®—æ­£ç¢ºè§’åº¦
-----------------
è¦åˆ©ç”¨å¤§å°é½’è¼ªçš„æ¯”ä¾‹é—œä¿‚, å¾å¤§åœ“çš„ angle ç®—å‡ºå°åœ’çš„æ—‹è½‰è§’åº¦ã€‚å› ç‚ºåœ“å‘¨èˆ‡åŠå¾‘æˆæ­£æ¯”, æ‰€ä»¥ç›´æ¥ç…§æ¯”ä¾‹çµ¦è§’åº¦å³å¯ã€‚
```Processing
void setup(){
  size(500,500);
  stroke(255);
  noFill();
}
float r0=225, r1=50;
float cx=250, cy=250;
void draw(){
  background(0);
  ellipse(cx,cy, r0*2, r0*2);
  float angle=atan2(mouseY-cy,mouseX-cx);
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);
  line(cx,cy, x, y);
  circle2(x,y, r1, angle2);
}
void circle2(float cx, float cy, float r, float angle){
  ellipse(cx,cy, r*2, r*2);
  for(float a=angle; a<angle+PI*2;a+=PI/4){
    line(cx,cy, cx+r*cos(a), cy+r*sin(a));
  }
}
```

Step07 ç•«å‡ºå°åœ“å­”å‹•çš„è»Œè·¡
----------------------
å…¶å¯¦å¯ä»¥åˆ©ç”¨ç›¸ä¼¼çš„ã€Œå¤§åœ“ã€å°åœ“ã€é—œä¿‚, è®“å°åœ“è£¡çš„å­”æ´è¦–ç‚ºæ›´å°çš„åŠå¾‘çš„å°å°åœ“, ä¾†ç•«å‡ºä¾†ç•«å‡ºç·šæ¢ã€‚é€™è£¡å…ˆåˆ©ç”¨ArrayList, ä¾†å­˜é»çš„åº§æ¨™, ä¸¦ç•«å‡ºå°æ‡‰çš„æ›²ç·šã€‚

Bug: ä¸éå› ç‚º atan2() æœ‰ç¯„åœ, è¶…éçš„è§’åº¦, å°±æ²’è¾¦æ³•ç•«å‡ºä¾†ã€‚
```Processing
ArrayList<PVector> points;
void setup(){
  size(500,500);
  stroke(255);
  noFill();
  points = new ArrayList<PVector>();
}
float r0=225, r1=50, r2=37;
float cx=250, cy=250;
void draw(){
  background(0);
  ellipse(cx,cy, r0*2, r0*2);
  beginShape();
  for( PVector pt : points ){
    vertex(pt.x, pt.y);//ä¹‹å¾Œå¯è®Šå½©è‰²æ¼¸å±¤è‰²å½©
  }
  endShape();
  float angle=atan2(mouseY-cy,mouseX-cx);
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);
  line(cx,cy, x, y);
  circle2(x,y, r1, angle2);
}
void circle2(float cx, float cy, float r, float angle){
  ellipse(cx,cy, r*2, r*2);
  for(float a=angle; a<angle+PI*2;a+=PI/4){
    line(cx,cy, cx+r*cos(a), cy+r*sin(a));
  }
}
void mouseDragged(){//æŒ‰ä¸‹mouseæ‰é–‹å§‹è¨˜éŒ„é»
  float angle=atan2(mouseY-cy,mouseX-cx);
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);  
  float x2=x+r2*cos(angle2), y2=y+r2*sin(angle2); 
  points.add( new PVector(x2,y2) );
}
```

Step08 è®“è§’åº¦æ²’ç¯„åœé™åˆ¶
--------------------
å› å‰ä¸€ç‰ˆçš„ç¨‹å¼ä½¿ç”¨ atan2() è®“è§’åº¦æœ‰å•é¡Œã€‚æ‰€ä»¥å…¶å¯¦å¯ä»¥åˆ©ç”¨ "è§’åº¦éå¢" çš„æ–¹å¼, ä¾†ä¿®æ”¹ angle è§’åº¦ã€‚ (mouseX,mouseY) vs. (pmouseX,pmouseY) æœƒæœ‰å¤¾è§’, æ‹¿é€™å€‹å¤¾è§’ä¾†è¨ˆç®—ã€‚
æ³¨æ„: ä¸èƒ½ angleNow - angle, è¦ angleNow-angleOldæ‰è¡Œã€‚å› ç‚ºé€™æ¨£æ‰æ˜¯2å€‹ -PI~+PI çš„æ•¸å­—ç›¸æ¯”è¼ƒ, æ‰æœƒæ­£ç¢ºã€‚

ä¸éé€™å€‹ç‰ˆæœ¬æœƒæœ‰1å€‹Bug: mouseå‹•å¤ªå¿«æ™‚, points åŠ å¾—ä¸å¤ å¯†, æœƒæœ‰æ¼é»çš„ç›´ç·šå‡ºç¾ã€‚
å¯èƒ½éœ€è¦ä»¥å¤ ç´°çš„è§’åº¦ç‚ºæº–, å…§æ’å‡ºæ›´å¤šçš„è§’åº¦,ä¾†æ¨ç®—å‡ºé»ã€‚ (é€™å€‹ä¹Ÿç®—æ˜¯ feature, å› ç‚ºä¸€å®šæœƒæœ‰äººæƒ³è¦ç•«å¾—å¾ˆå¿«, å°±è®“ä»–å€‘çœ‹åˆ°ç•«é¢å¤±æ•—çš„ç·šæ¢å§) 
TODO: çœŸçš„é»å¤ªå¤šæ™‚, memeoryä¹Ÿæœƒç”¨ç›¡, éœ€è¦æä¾›è­¦å‘Š, ä¸¦å°‡ memory æ¸…ç©º
TODO: æ¯æ¬¡ mousePressedæ™‚, æ‡‰è©²æ˜¯æ–°çš„ç·šæ®µã€‚æ¯æ¬¡ mouseReleasedæ™‚, å°±çµæŸç·šæ®µã€‚
TODO: ç•«åœ–å‰/ç•«åœ–ä¸­, å¯æä¾›ã€Œæ›é½’è¼ªã€çš„åŠŸèƒ½ã€‚

```Processing
ArrayList<PVector> points;
void setup(){
  size(500,500);
  stroke(255);
  noFill();
  points = new ArrayList<PVector>();
  angle = atan2(-cy, -cx);//ä¸€é–‹å§‹mouseX,mouseYç‚º0,æ‰€ä»¥è§’åº¦å‘å·¦ä¸Šè§’
}//ä¹‹å¾Œå¯å¼•å° mousePressed åœ¨å°é½’è¼ªå¾Œ, æ‰èƒ½é–‹å§‹æ§åˆ¶è½‰å‹•
float r0=225, r1=50, r2=37;
float cx=250, cy=250;
float angle;
void draw(){
  background(0);
  ellipse(cx,cy, r0*2, r0*2);
  beginShape();
  for( PVector pt : points ){
    vertex(pt.x, pt.y);//ä¹‹å¾Œå¯è®Šå½©è‰²æ¼¸å±¤è‰²å½©
  }
  endShape();
  float delta = deltaAngle();
  angle += delta;
  println(angle, delta);
  //float angle=atan2(mouseY-cy,mouseX-cx);
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);
  line(cx,cy, x, y);
  circle2(x,y, r1, angle2);
}
void circle2(float cx, float cy, float r, float angle){
  ellipse(cx,cy, r*2, r*2);
  for(float a=angle; a<angle+PI*2;a+=PI/4){
    line(cx,cy, cx+r*cos(a), cy+r*sin(a));
  }
}
float deltaAngle(){
  float angleNow=atan2(mouseY-cy,mouseX-cx);
  float angleOld=atan2(pmouseY-cy,pmouseX-cx);
  //ä¸èƒ½ angleNow - angle, è¦ angleNow-angleOldæ‰è¡Œ
  float delta = angleNow - angleOld;
  if( abs(delta)> PI ){
    if(delta>0) delta-=PI*2;
    else delta += PI*2;
  }
  return delta;
}
void mouseDragged(){//æŒ‰ä¸‹mouseæ‰é–‹å§‹è¨˜éŒ„é»
  //float angle=atan2(mouseY-cy,mouseX-cx);
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);  
  float x2=x+r2*cos(angle2), y2=y+r2*sin(angle2); 
  points.add( new PVector(x2,y2) );
}
```

Step09 å½©è‰²çš„ç·šæ¢
---------------
ç›®å‰åšå‡ºå½©è‰²çš„ç·šæ¢, ç‚ºäº†éŒ„å½± saveFrame() æ‰€ä»¥æ”¹æˆ 640x480 çš„è§£æåº¦ã€‚ä¸éæœ‰äº›å¯æ”¹é€²çš„åœ°æ–¹:
- å¯ä»¥ç”¨å…¶ä»–è§£æåº¦ (ex. IG çš„æ­£æ–¹å‹, FBçš„é•·æ–¹å½¢) æ–¹ä¾¿åˆ†äº«å½±ç‰‡
- ç•«å‡ºä¾†çš„ç·šæœ‰é»ç²—, å¯èƒ½æ˜¯è§£æåº¦ 640x480 ä¸å¤ é«˜
- Processing ç”¨ 2010 QuickTime Movie Maker åšå‡ºä¾†çš„ mov æª”, ç„¡æ³•åœ¨ Windows é›»å½±èˆ‡é›»è¦–æ’­æ”¾(ç„¡æ³•æ’­æ”¾: æ­¤é …ç›®çš„ç·¨ç¢¼æ ¼å¼æ˜¯ä¸æ”¯æ´çš„æ ¼å¼ã€‚0xc00d5212)ã€‚ ä½¿ç”¨ Messenger ä¹Ÿç„¡æ³•å‚³é€ã€‚æˆ‘ç›®å‰æ˜¯å…ˆåœ¨ FBè²¼æ–‡å¾Œ, å†ä¸‹è¼‰ mp4 æª”ã€‚
- Can't play: This item was encoded in a format that's not supported. 0xc00d5212

[![Spirograph](https://j.gifs.com/6XrAw7.gif)](https://youtu.be/ELrkairuidA)

```Processing
ArrayList<PVector> points;
void setup(){
  size(640,480,P2D);
  stroke(255);
  noFill();
  points = new ArrayList<PVector>();
  angle = atan2(-cy, -cx);//ä¸€é–‹å§‹mouseX,mouseYç‚º0,æ‰€ä»¥è§’åº¦å‘å·¦ä¸Šè§’
}//ä¹‹å¾Œå¯å¼•å° mousePressed åœ¨å°é½’è¼ªå¾Œ, æ‰èƒ½é–‹å§‹æ§åˆ¶è½‰å‹•
float r0=225, r1=57, r2=37.3;
float cx=320, cy=240;
float angle;
void draw(){
  background(0);
  ellipse(cx,cy, r0*2, r0*2);
  colorMode(HSB);
  float H=0;
  beginShape();
  for( PVector pt : points ){
    stroke( H, 255,255);
    vertex(pt.x, pt.y);//ä¹‹å¾Œå¯è®Šå½©è‰²æ¼¸å±¤è‰²å½©
    H+=1;
    if(H>255) H-=255;
  }
  endShape();
  colorMode(RGB);
  stroke(255);
  angle += deltaAngle();
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);
  line(cx,cy, x, y);
  circle2(x,y, r1, angle2);
  //if(mousePressed) saveFrame();
}
void circle2(float cx, float cy, float r, float angle){
  ellipse(cx,cy, r*2, r*2);
  for(float a=angle; a<angle+PI*2;a+=PI/4){
    line(cx,cy, cx+r*cos(a), cy+r*sin(a));
  }
}
float deltaAngle(){
  float angleNow=atan2(mouseY-cy,mouseX-cx);
  float angleOld=atan2(pmouseY-cy,pmouseX-cx);
  float delta = angleNow - angleOld;
  if( abs(delta)> PI ){
    if(delta>0) delta-=PI*2;
    else delta += PI*2;
  }
  return delta;
}
void mouseDragged(){//æŒ‰ä¸‹mouseæ‰é–‹å§‹è¨˜éŒ„é»
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);  
  float x2=x+r2*cos(angle2), y2=y+r2*sin(angle2); 
  points.add( new PVector(x2,y2) );
}
```

Step10 æº–å‚™å¯åˆ®èŠ±çš„èƒŒæ™¯åœ–
----------------------
åœ¨è¨˜éŒ„ç·šæ¢æ™‚, è¦ç”¨å¾ˆé•·çš„é™£åˆ—ä¾†ç•«ç·š, å¥½åƒæœ‰é»éº»ç…©ã€‚ç‚ºäº†è®“ç·šæ¢æœ‰æ¼¸å±¤çš„è‰²å½©, æˆ–è¨±å¯æ”¹ç”¨åˆ®ç•«çš„æ–¹å¼ä¾†åš, ä¹Ÿå°±æ˜¯é»‘è‰²èƒŒæ™¯è¢«åˆ®æ‰å¾Œ, ä¾¿æœƒç§€å‡ºè—åœ¨å¾Œé¢çš„æ¼¸å±¤èƒŒæ™¯åœ–ã€‚
é€™å€‹ç‰ˆæœ¬å…ˆæŠŠåœ“è®Šç´°ã€æ›²ç·šè®Šç²—ã€æŠŠèƒŒæ™¯æ›åœ–ã€‚ä¹‹å¾Œå†æƒ³æ€éº¼åœ¨è“‹ä¸Šå¯åˆ®èŠ±çš„maskã€‚
```Processing
PImage imgBG;
//https://gentlejourneysbirthing.com/home/colorful_watercolor_texture_by_connyduck-d6o409f/
ArrayList<PVector> points;
void setup(){
  size(800,600,P2D);
  imgBG=loadImage("watercolor_texture.png");
  stroke(255);
  noFill();
  points = new ArrayList<PVector>();
  angle = atan2(-cy, -cx);//ä¸€é–‹å§‹mouseX,mouseYç‚º0,æ‰€ä»¥è§’åº¦å‘å·¦ä¸Šè§’
}//ä¹‹å¾Œå¯å¼•å° mousePressed åœ¨å°é½’è¼ªå¾Œ, æ‰èƒ½é–‹å§‹æ§åˆ¶è½‰å‹•
float r0=225, r1=57, r2=37.3;
float cx=320, cy=240;
float angle;
void draw(){
  background(imgBG);
  colorMode(RGB);
  stroke(255);
  strokeWeight(1);
  ellipse(cx,cy, r0*2, r0*2);
  angle += deltaAngle();
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);
  //line(cx,cy, x, y);//ä¸è¦ç•«ç·š,æ¯”è¼ƒå¥½çœ‹
  circle2(x,y, r1, angle2);
  colorMode(HSB);
  float H=0;
  strokeWeight(3);
  beginShape();
  for( PVector pt : points ){
    stroke( H, 255,255);
    vertex(pt.x, pt.y);//ä¹‹å¾Œå¯è®Šå½©è‰²æ¼¸å±¤è‰²å½©
    H+=1;
    if(H>255) H-=255;
  }
  endShape();
  //if(mousePressed) saveFrame();
}
void circle2(float cx, float cy, float r, float angle){
  ellipse(cx,cy, r*2, r*2);
  for(float a=angle; a<angle+PI*2;a+=PI/4){
    line(cx,cy, cx+r*cos(a), cy+r*sin(a));
  }
}
float deltaAngle(){
  float angleNow=atan2(mouseY-cy,mouseX-cx);
  float angleOld=atan2(pmouseY-cy,pmouseX-cx);
  float delta = angleNow - angleOld;
  if( abs(delta)> PI ){
    if(delta>0) delta-=PI*2;
    else delta += PI*2;
  }
  return delta;
}
void mouseDragged(){//æŒ‰ä¸‹mouseæ‰é–‹å§‹è¨˜éŒ„é»
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);  
  float x2=x+r2*cos(angle2), y2=y+r2*sin(angle2); 
  points.add( new PVector(x2,y2) );
}
```

Step11 ç·´ç¿’åˆ®ç•«
---------------
æˆ‘å…ˆå¯«å€‹ç°¡å–®çš„ç¨‹å¼, å¯ä»¥åœ¨é»‘è‰²èƒŒæ™¯ä¸Š, æ”¾æœ‰åˆ®ç—•çš„å½©è‰²åœ–, çœ‹èµ·ä¾†çš„æ•ˆæœ,åƒæ˜¯åˆ®ç•«ã€‚
```Processing
PImage imgBG;
PGraphics pg;
void setup(){
  size(800,600);
  imgBG=loadImage("watercolor_texture.png");
  pg = createGraphics(800,600);
  pg.beginDraw();
  pg.noFill();
  pg.strokeWeight(10);
  pg.stroke(255);
  pg.ellipse(400,300,300,300);
  pg.endDraw();
}
void draw(){
  background(0);
  imgBG.mask(pg);
  image(imgBG,0,0);
}
```

æ¥è‘—, ä¾¿èƒ½å°‡ç¨‹å¼æ”¹ç‰ˆå‡ºä¾†äº†ã€‚
[![Spirograph](https://j.gifs.com/lxKWVj.gif)][https://user-images.githubusercontent.com/3252557/112619853-c18b4080-8e62-11eb-9f74-ff68881f5c5f.mp4]

```Processing
PImage imgBG;
PGraphics pg;
//https://gentlejourneysbirthing.com/home/colorful_watercolor_texture_by_connyduck-d6o409f/
ArrayList<PVector> points;
void setup(){
  size(800,600,P2D);  
  stroke(255);
  noFill();
  points = new ArrayList<PVector>();
  angle = atan2(-cy, -cx);//ä¸€é–‹å§‹mouseX,mouseYç‚º0,æ‰€ä»¥è§’åº¦å‘å·¦ä¸Šè§’
  imgBG=loadImage("watercolor_texture.png");
  pg = createGraphics(800,600);
  drawPG();
}//ä¹‹å¾Œå¯å¼•å° mousePressed åœ¨å°é½’è¼ªå¾Œ, æ‰èƒ½é–‹å§‹æ§åˆ¶è½‰å‹•
float r0=225, r1=57, r2=37.3;
float cx=320, cy=240;
float angle;
void drawPG(){
  pg.beginDraw();
  pg.noFill();
  pg.strokeWeight(2);
  pg.stroke(255);
  pg.beginShape();
  int a = points.size();
  if(a>2){
    pg.vertex(points.get(a-2).x, points.get(a-2).y);
    pg.vertex(points.get(a-1).x, points.get(a-1).y);
  }//åªç•«æœ€å¾Œçš„é»,å‰é¢ä¸ç”¨å†ç•«,å¯èƒ½æœƒå¿«ä¸€é»
/*  for( PVector pt : points ){
    pg.vertex(pt.x, pt.y);//ä¹‹å¾Œå¯è®Šå½©è‰²æ¼¸å±¤è‰²å½©
  }*/
  pg.endShape();
  pg.endDraw();
}
void draw(){
  background(0);
  imgBG.mask(pg);
  image(imgBG,0,0);
  colorMode(RGB);
  stroke(128);
  strokeWeight(1);
  ellipse(cx,cy, r0*2, r0*2);
  angle += deltaAngle();
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);
  //line(cx,cy, x, y);//ä¸è¦ç•«ç·š,æ¯”è¼ƒå¥½çœ‹
  circle2(x,y, r1, angle2);
  //if(mousePressed) saveFrame();
}
void circle2(float cx, float cy, float r, float angle){
  ellipse(cx,cy, r*2, r*2);
  for(float a=angle; a<angle+PI*2;a+=PI/4){
    line(cx,cy, cx+r*cos(a), cy+r*sin(a));
  }
}
float deltaAngle(){
  float angleNow=atan2(mouseY-cy,mouseX-cx);
  float angleOld=atan2(pmouseY-cy,pmouseX-cx);
  float delta = angleNow - angleOld;
  if( abs(delta)> PI ){
    if(delta>0) delta-=PI*2;
    else delta += PI*2;
  }
  return delta;
}
void mouseDragged(){//æŒ‰ä¸‹mouseæ‰é–‹å§‹è¨˜éŒ„é»
  float angle2= -angle * r0 / r1;
  float x=cx+(r0-r1)*cos(angle), y=cy+(r0-r1)*sin(angle);  
  float x2=x+r2*cos(angle2), y2=y+r2*sin(angle2); 
  points.add( new PVector(x2,y2) );
  drawPG();
}
```
